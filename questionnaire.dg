<*
DISCLAIMER:
NOT TO BE USED AS A LEGAL ADVICE
*>
[#import ppra:    ppra.dg    ]
[#import medical: medical.dg ]
[#import ferpa:   ferpa.dg   ]
[#import dppa:    dppa.dg    ]
[#import gov:     government-records.dg ]
[>global-start< ask:
  {text: Do the data concern living persons?}
  {answers:
    {yes:
      [set: Code=green] <-- Explicit settings of colors is going away soon!
      [>data-origin< ask:
        {text: Where was the data collected?}
        {answers:
          {in the US: [set: Origin += US]}
          {outside of the US: [set: Origin += nonUS]}
          {both: [set: Origin += US, nonUS]}
        }
      ]
      [when:
        { Origin+=US : [call:usLegislation] } ]
      [when:
        { Origin+=nonUS : [call:nonUsLegislation] }
      ]
    }
    {no:  [set: Code=blue; Identity=noPersonData] }
}]
[call: dua]
[end]

[>usLegislation< section:
  {title: US legislation}
  [>filter-medical< ask:
    {text: Do the data contain health or medical information?}
    {answers:
      {yes: [call: medical>main]}}
  ]
  [>filter-ppra< ask:
    {text: Was the data collected from students in an elementary or secondary school?}
    {answers:
      {yes: [call: ppra>main]}
    }
  ]
  [>filter-ferpa< ask:
    {text: Do the data contain any information derived from ministry of education data?}
    {answers:
      {yes: [call: ferpa>main]}
    }]
  [call: gov>main]
  [call: dppa>main]
  [>futures< todo: Future additions go here.]
  [>catch-all< ask:
    {text: Is there any other legislation that applies to this dataset?}
    {answers:
      {formal no:}
      {yes: [set: AdditionalRestrictions += manualInspectionRequired]}
    }
  ]
]
[>usl-e< end]

[>nonUsLegislation< section:
  {title: Non-US Legislation}
  [set: AdditionalRestrictions += manualInspectionRequired]
  [>nonUsStatement< ask:
    {text: Please consult with your local data privacy experts to make sure they meet your local requirements.}
    {answers:
        {ok:}
    }
  ]
]
[>nusl-e< end]

[>dua< section:
  {title: Data Use Agreements}
  [>dua-ext< todo: Data use agreements]
  [>duaTimeLimit< ask:
    {text: Is there any reason why we cannot store the data indefinitely? }
    {answers:
      {no: [set: TimeLimit=none] }
      {yes:
        [ask:
          {text:For how long should we keep the data?}
          {answers:
            { indefinitely: [set: TimeLimit=none]  }
            { 50 years:     [set: TimeLimit=_50yr] }
            { 5 years:      [set: TimeLimit=_5yr]  }
            { 2 years:      [set: TimeLimit=_2yr]  }
            { 1 year:       [set: TimeLimit=_1yr]  }
          }]}}]
  [>duaAdditional< ask:
    {text: Did the data have any restrictions on sharing, e.g. stated in an agreement or policy statement?}
    {answers:
      {yes: [set: AdditionalRestrictions+=contractOrPolicy]}
    }
  ]
]
[end]
<*
DISCLAIMER:
NOT TO BE USED AS A LEGAL ADVICE
*>
